clear

! This is a 6 node syste which is a modified version of the IEEE 4 Node Test System with a Grd. Y/D step down

new circuit.4busYDBal   basekV=12.47 phases=3 bus1 = sourcebus
~ mvasc3=200000 200000

Set earthmodel=carson

! **** DEFINE WIRE DATA 
new wiredata.conductor Runits=mi Rac=0.306 GMRunits=ft GMRac=0.0244  Radunits=in Diam=0.721 
new wiredata.neutral   Runits=mi Rac=0.592 GMRunits=ft GMRac=0.00814 Radunits=in Diam=0.563 

! **** DEFINE LINE GEOMETRY; REDUCE OUT THE NEUTRAL
new linegeometry.4wire nconds=4 nphases=3 reduce=yes 
~ cond=1 wire=conductor units=ft x=-4   h=28 
~ cond=2 wire=conductor units=ft x=-1.5 h=28 
~ cond=3 wire=conductor units=ft x=3    h=28 
~ cond=4 wire=neutral   units=ft x=0    h=24 

! **** 12.47 KV LINE
new line.line1 geometry=4wire length=2000 units=ft bus1=sourcebus bus2=n2

! **** 3-PHASE STEP-DOWN TRANSFORMER 12.47/4.16 KV ungrdY-Delta
new transformer.t1 xhl=6
~ wdg=1 bus=n2.1.2.3.0 conn=wye     kV=12.47 kVA=6000 %r=0.5  ! Ground the neutral of wye winding
~ wdg=2 bus=n3         conn=Delta   kV=4.16  kVA=6000 %r=0.5 

! **** 4.16 KV LINE
new line.line2 bus1=n3 bus2=n4 geometry=4wire length=2500 units=ft  
new line.line3 bus1=n4 bus2=n5 geometry=4wire length=1000 units=ft
new line.line4 bus1=n5 bus2=n6 geometry=4wire length=1000 units=ft  

! **** DEFINE LOADS
new load.load1 phases=3 bus1=n4 conn=Delta kV=4.16 kW=5400 pf=0.9  model=1
~ vminpu=0.7    ! model will remain const p,q down to 0.75 pu voltage    

new load.load2 phases=3 bus1=n6 conn=Delta kV=4.16 kW=600 pf=0.9  model=1
~ vminpu=0.7    ! model will remain const p,q down to 0.75 pu voltage    

! **** PADMOUNT TRANSFORMER
new transformer.t2 xhl=2
~ wdg=1 bus=n5.1.2.3.0 conn=wye kV=4.16 kVA=500 %r=0.55
~ wdg=2 bus=IMbus.1.2.3.0 conn=wye kV=0.48 kVA=500 %r=0.55 

!MOTOR DEFINITIONS
New indmach012.IM1 Bus1=IMBus.1.2.3 Phases = 3 kW = 372 kV = 0.48 kVA = 478 MaxSlip = 0.1 Slip = 0.005 
~ puRs = 0.007 puRr = 0.0062 puXs = 0.0409 puXr = 0.0267 puXm = 3.62 H = 3.34 D = 1


set voltagebases=[12.47, 4.16, 0.48] 
calcvoltagebases    
solve

! Various reports ...
!Show Voltage  LL Nodes
show voltages LN Nodes 
!show currents resid=yes elements  ! this shows sum of phase currents
!Show Powers kva Elements


